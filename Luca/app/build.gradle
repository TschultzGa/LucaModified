apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'kotlin-android'
apply plugin: 'org.sonarqube'
apply plugin: 'org.owasp.dependencycheck'
apply from: 'dependencies.gradle'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"
    defaultConfig {
        applicationId "de.culture4life.luca"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 87
        versionName "2.0.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        culture4life {
            keyAlias project.getProperties().getOrDefault("C4L_SIGNING_KEY_ALIAS", '"<signing key alias>"')
            keyPassword project.getProperties().getOrDefault("C4L_SIGNING_KEY_PASSWORD", '"<signing key password>"')
            storeFile file(project.getProperties().getOrDefault("C4L_SIGNING_STORE_FILE", '"<signing store file>"'))
            storePassword project.getProperties().getOrDefault("C4L_SIGNING_STORE_PASSWORD", '"<signing store password>"')
        }
        debug {
            keyAlias "debug"
            storeFile file("../keystore/culture4life_debug_keystore.jks")
            storePassword "J2j8EuSBeNarHEmbpAKW"
            keyPassword "A2mtGm8t4U4Ep4Jz3cmk"
        }
    }
    buildTypes {
        production {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.culture4life
            matchingFallbacks = ['release']
            buildConfigField "String", "API_BASE_URL", '"https://app.luca-app.de"'
            buildConfigField "String", "STAGING_API_USERNAME", '""'
            buildConfigField "String", "STAGING_API_PASSWORD", '""'
            buildConfigField "String", "COMMIT_HASH", project.getProperties().getOrDefault("COMMIT_HASH", '"<commit hash>"')
        }
        debug {
            debuggable true
            minifyEnabled false
            versionNameSuffix " Dev"
            applicationIdSuffix ".dev"
            signingConfig signingConfigs.debug
            matchingFallbacks = ['debug']
            buildConfigField "String", "API_BASE_URL", '"https://app-dev.luca-app.de"'
            buildConfigField "String", "STAGING_API_USERNAME", project.getProperties().getOrDefault("STAGING_API_USERNAME", '"<staging username>"')
            buildConfigField "String", "STAGING_API_PASSWORD", project.getProperties().getOrDefault("STAGING_API_PASSWORD", '"<staging password>"')
            buildConfigField "String", "COMMIT_HASH", project.getProperties().getOrDefault("COMMIT_HASH", '"<commit hash>"')
        }
        qs {
            initWith debug
            versionNameSuffix " QS"
            applicationIdSuffix ".qs"
            buildConfigField "String", "API_BASE_URL", '"https://app-qs.luca-app.de"'
        }
        aqs {
            initWith debug
            versionNameSuffix " AQS"
            applicationIdSuffix ".aqs"
            buildConfigField "String", "API_BASE_URL", '"https://app-aqs.luca-app.de"'
        }
        hotfix {
            initWith debug
            versionNameSuffix " Hotfix"
            applicationIdSuffix ".hotfix"
            buildConfigField "String", "API_BASE_URL", '"https://app-hotfix.luca-app.de"'
        }
        pentest {
            initWith debug
            versionNameSuffix " Pentest"
            applicationIdSuffix ".pentest"
            buildConfigField "String", "API_BASE_URL", '"https://app-pentest.luca-app.de"'
        }
        p1 {
            initWith debug
            versionNameSuffix " P1"
            applicationIdSuffix ".p1"
            buildConfigField "String", "API_BASE_URL", '"https://app-p1.luca-app.de"'
        }
        p2 {
            initWith debug
            versionNameSuffix " P2"
            applicationIdSuffix ".p2"
            buildConfigField "String", "API_BASE_URL", '"https://app-p2.luca-app.de"'
        }
        p3 {
            initWith debug
            versionNameSuffix " P3"
            applicationIdSuffix ".p3"
            buildConfigField "String", "API_BASE_URL", '"https://app-p3.luca-app.de"'
        }
        demo {
            initWith debug
            versionNameSuffix " Demo"
            applicationIdSuffix ".demo"
            buildConfigField "String", "API_BASE_URL", '"https://app-demo.luca-app.de"'
        }
        release {
            initWith debug
            versionNameSuffix " Release"
            applicationIdSuffix ".release"
            buildConfigField "String", "API_BASE_URL", '"https://app-release.luca-app.de"'
        }
        preprod {
            initWith debug
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            versionNameSuffix " Preprod"
            applicationIdSuffix ".preprod"
            buildConfigField "String", "API_BASE_URL", '"https://app-preprod.luca-app.de"'
        }
    }
    buildFeatures {
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }
    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = false
        }
        unitTests.all {
            jvmArgs '-noverify'
        }
    }
    packagingOptions {
        exclude 'META-INF/*'
        exclude 'draftv3/schema'
        exclude 'draftv4/schema'
    }
}

play {
    serviceAccountCredentials.set(file("service-account.json"))
    defaultToAppBundles.set(true)
    track.set("internal")
}

sonarqube {
    properties {
        property "sonar.projectName", "luca Android"
        property "sonar.projectKey", "luca:de.culture4life.luca.android"
        property "sonar.branch.name", project.getProperties().getOrDefault("BRANCH_NAME", '""')
        property "sonar.host.url", project.getProperties().getOrDefault("SONAR_HOST_URL", '"http://localhost:9000"')
        property "sonar.login", project.getProperties().getOrDefault("SONAR_AUTH_TOKEN", '""')
    }
}
dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation project(':decoder')

    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.camera:camera-camera2:1.0.1'
    implementation 'androidx.camera:camera-lifecycle:1.0.1'
    implementation 'androidx.camera:camera-view:1.0.0-alpha27'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'
    implementation 'androidx.work:work-runtime:2.5.0'
    implementation 'androidx.work:work-rxjava3:2.5.0'

    implementation bouncycastle
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.12.4'
    implementation 'com.github.akarnokd:rxjava3-extensions:3.0.1'
    implementation 'com.github.akarnokd:rxjava3-bridge:3.0.0'
    implementation 'com.github.kenglxn.QRGen:android:2.6.0'
    implementation 'com.github.neXenio:RxKeyStore:0.6.4'
    implementation('com.github.neXenio:RxPreferences:1.2.0') {
        exclude group: 'net.grandcentrix.tray', module: 'tray'
    }
    implementation 'com.github.Nivador:RxPermissions:0.11.2'
    implementation 'com.github.numerative:Five-Star-Me:2.1.0'
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.google.guava:guava:27.0.1-jre'
    implementation 'com.google.mlkit:barcode-scanning:17.0.0'
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.11.1'

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.ncorti:slidetoact:0.9.0'
    implementation 'com.networknt:json-schema-validator:1.0.53'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.tbuonomo:dotsindicator:4.2'
    implementation 'com.uber.rxdogtag2:rxdogtag:2.0.1'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2'
    runtimeOnly('io.jsonwebtoken:jjwt-orgjson:0.11.2') {
        exclude group: 'org.json', module: 'json' // provided by Android natively
    }
    implementation 'io.reactivex.rxjava3:rxjava:3.1.0'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation jackson_dataformat_cbor
    implementation 'joda-time:joda-time:2.10.10'
    implementation 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:2.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"


    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'com.android.support.test:rules:1.0.2'
    testImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation junit
    testImplementation 'net.lachlanmckee:timber-junit-rule:1.0.1'
    testImplementation 'org.mockito:mockito-core:2.21.0'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.0-beta.5'
    testImplementation 'org.powermock:powermock-classloading-xstream:2.0.0-beta.5'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.0-beta.5'
    testImplementation 'org.powermock:powermock-module-junit4-common:2.0.0-beta.5'
    testImplementation 'org.powermock:powermock-module-junit4-rule:2.0.0-beta.5'
    testImplementation 'org.robolectric:robolectric:4.5.1'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'net.lachlanmckee:timber-junit-rule:1.0.1'
    androidTestImplementation 'org.mockito:mockito-android:2.21.0'
}
